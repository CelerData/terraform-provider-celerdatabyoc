---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "Provision CelerData Cloud Private on Azure"
subcategory: ""
description: |-
  
---

# Provision CelerData Cloud Private on Azure

This article walks you through the following steps necessary to deploy a CelerData Cloud Private cluster on Azure:

- [Preparations](#preparations)
- [Configure providers](#configure-providers)
- [Configure Azure objects](#configure-azure-objects)
- [Describe infrastructure](#describe-infrastructure)
- [Apply configurations](#apply-configurations)

Read this article before you start a Terraform configuration for your cluster deployment on Azure.

## Preparations

Before using the CelerData Cloud Private provider to create infrastructure at the Azure account level for the first time, you must complete the following preparations:

### For Azure

For Azure, you need to:

1. Have an Azure service account with administrative privileges.
2. Install the Azure CLI. For more information, see [How to install the Azure CLI](https://learn.microsoft.com/en-us/cli/azure/install-azure-cli).

### For CelerData

For CelerData, you need to obtain CelerData credentials.

The CelerData Cloud Private provider will use the **Secret** and **Client ID** of your application key to provision and manage CelerData resources. Follow these steps to obtain these credentials:

1. Sign in to the [CelerData Cloud Private console](https://cloud.celerdata.com/login).

2. In the left-side navigation pane, choose **Application keys**.

3. On the **Application keys** page, click **New secret**.

4. In the dialog box that appears, optionally enter a description, and then click **Generate now** to generate an application key. Then, copy the **Secret** and **Client ID** before you close the dialog box.

   ~> The **Secret** can be viewed only when the application key is created. Make sure that you copy and save the **Secret** before closing the dialog box.

For more information about managing application keys, see [Application keys](https://docs.celerdata.com/private/main/security/application_keys).

### For Terraform

For Terraform, you need to:

1. Install [Terraform](https://developer.hashicorp.com/terraform/downloads) in your terminal.

2. Have a Terraform project. In your terminal, create an empty directory (for example, `terraform`) and then switch to it. (Each separate set of Terraform configuration files must be in its own directory, which is called a Terraform project.)

## Configure providers

This section assumes that you have [completed the preparations](#preparations).

Create a **.tf** file (for example, **main.tf**) in your Terraform project. Then, add the following code snippet to the **.tf** file:

```terraform
terraform {
  required_providers {
    celerdatabyoc = {
      source  = "CelerData/celerdatabyoc"
      version = "<provider_version>"
    }
    azuread = {
      source  = "hashicorp/azuread"
      version = "2.47.0"
    }
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "=3.0.0"
    }
  }
}

provider "celerdatabyoc" {
  client_id = "<client_id>"
  client_secret = "<client_secret>"
}

locals {
  cluster_region = "<Azure_region_ID>"
  azure_region   = "<Azure_region_name>"
}
```

The parameters you need to specify are as follows:

- `provider_version`: Enter the CelerData provider version of your choice. We recommend that you select the latest provider version. You can view the provider versions offered by CelerData Cloud Private from the [CelerData Cloud Private provider](https://registry.terraform.io/providers/CelerData/celerdatabyoc/latest/docs) page.
- `client_id` and `client_secret`: Enter the **Client ID** and **Secret** of your application key. See [For CelerData](#for-celerdata).
- `cluster_region` and `azure_region`: Enter the ID (for example, `eastus`) and name (for example, `East US`), respectively, of the Azure region in which you want your CelerData cluster to run. See [Supported cloud platforms and regions](https://docs.celerdata.com/private/main/get_started/cloud_platforms_and_regions#azure). The Azure region you specify here must be the same as the Azure region of the resource group you have created in [Configure Azure objects](#configure-azure-objects).

## Configure Azure objects

This section assumes that you have [completed the preparations](#preparations) and have [configured the providers](#configure-providers).

The cluster deployment on Azure depends on the following Azure objects:

- A resource group, which will host all the Azure resources required for the cluster deployment, including a storage account and a container within the storage account, a managed identity, a virtual network and a subnet within the virtual network, a security group, and an SSH public key.
- A storage account and a container within the storage account, which will be used to store your data.
- A managed identity, to which you also need to grant the required permissions, so the cluster will be able to store query profiles to the container.
- An app registration to authorize Terraform as a service principal, and a client secret for the registered application. You also need to add role assignments to the application, so Terraform can launch the resources necessary to deploy the cluster within your Azure service account.
- An SSH public key, which gives access to your virtual machines for automatic deployment, so Terraform can deploy the required service processes on your virtual machines.

  You need to create an SSH public key on your local computer, because you will need to fill the path in the `public_key` element of this object.

- A virtual network and a subnet within the virtual network for the virtual machines on which the cluster depends.
- A security group to which the subnet is assigned.

To create these Azure objects, you need to declare the following resources in the **.tf** file in which you have configured the providers:

```terraform
provider "azuread" {}

provider "azurerm" {
  features {}
  subscription_id = "<Microsoft_subscription_ID>"
}

# Create a resource group
resource "azurerm_resource_group" "example" {
  name     = "<resource_group_name>"
  location = local.azure_region
}

# Create a managed identity
resource "azurerm_user_assigned_identity" "example" {
  location            = azurerm_resource_group.example.location
  resource_group_name = azurerm_resource_group.example.name
  name                = "<managed_identity_name>"
}

# Assign permissions to the managed identity
locals {
  managed_identity_roles = [
    "Reader",
    "Storage Blob Data Contributor",
  ]
}
resource "azurerm_role_assignment" "assignment_identity_roles" {
  count                = length(local.managed_identity_roles)
  role_definition_name = local.managed_identity_roles[count.index]
  scope                = azurerm_resource_group.example.id
  principal_id         = azurerm_user_assigned_identity.example.principal_id
}

# Create a storage account
resource "azurerm_storage_account" "example" {
  name                     = "<storage_account_name>"
  resource_group_name      = azurerm_resource_group.example.name
  location                 = azurerm_resource_group.example.location
  account_tier             = "Standard"
  account_replication_type = "GRS"
}

# Create a storage container
resource "azurerm_storage_container" "example" {
  name                  = "<storage_container_name>"
  storage_account_name  = azurerm_storage_account.example.name
  container_access_type = "private"
}

# Create an app registration and a client secret for it
resource "azuread_application_registration" "example" {
  display_name     = "<app_registration_name>"
  description      = "My example application"
  sign_in_audience = "AzureADMyOrg"
}
resource "azuread_application_password" "example" {
  application_id = azuread_application_registration.example.id
  display_name   = "<app_secret_name>"
}
resource "azuread_service_principal" "app_service_principal" {
  client_id = azuread_application_registration.example.client_id
}

# Add role assignments to the app application
locals {
  app_registration_roles = [
    "Reader",
    "Virtual Machine Contributor",
    "Network Contributor",
    "Managed Identity Operator"
  ]
}
resource "azurerm_role_assignment" "assignment_app_roles" {
  count                = length(local.app_registration_roles)
  role_definition_name = local.app_registration_roles[count.index]
  scope                = azurerm_resource_group.example.id
  principal_id         = azuread_service_principal.app_service_principal.object_id
}

# Create an SSH public key
resource "azurerm_ssh_public_key" "example" {
  name                = "<ssh_key_name>"
  resource_group_name = azurerm_resource_group.example.name
  location            = azurerm_resource_group.example.location
  public_key          = file("~/.ssh/id_rsa.pub")
}

# Create a virtual network
resource "azurerm_virtual_network" "example" {
  name                = "<network_name>"
  location            = azurerm_resource_group.example.location
  resource_group_name = azurerm_resource_group.example.name
  address_space       = ["10.0.0.0/16"]
}

# Create a subnet
resource "azurerm_subnet" "example" {
  name                 = "<subnet_name>"
  resource_group_name  = azurerm_resource_group.example.name
  virtual_network_name = azurerm_virtual_network.example.name
  address_prefixes     = ["10.0.1.0/24"]
}

# Create a network security group
resource "azurerm_network_security_group" "example" {
  name                = "<security_group_name>"
  location            = azurerm_resource_group.example.location
  resource_group_name = azurerm_resource_group.example.name
}

# Assign the subnet to the security group
resource "azurerm_subnet_network_security_group_association" "example" {
  subnet_id                 = azurerm_subnet.example.id
  network_security_group_id = azurerm_network_security_group.example.id
}
```

See the following documents for more information:

- [azurerm_resource_group](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/resource_group)
- [azurerm_user_assigned_identity](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/user_assigned_identity)
- [azurerm_role_assignment](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/role_assignment)
- [azurerm_storage_account](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/storage_account)
- [azurerm_storage_container](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/storage_container)
- [azuread_application_registration](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs/resources/application_registration)
- [azuread_service_principal](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs/resources/service_principal)
- [azuread_application_password](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs/resources/application_password)
- [azurerm_ssh_public_key](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/ssh_public_key)
- [azurerm_virtual_network](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/virtual_network)
- [azurerm_subnet](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/subnet)
- [azurerm_subnet_network_security_group_association](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/subnet_network_security_group_association)

## Describe infrastructure

This section provides a sample infrastructure configuration that automates the deployment of a classic CelerData cluster on Azure to help you understand how you can work with the CelerData Cloud Private provider. It assumes that you have [completed the preparations](#preparations), [configured the providers](#configure-providers), and [configured the Azure objects](#configure-azure-objects).

To create a classic CelerData cluster, you need to declare the following resources, which represent the infrastructure to be built, in the **.tf** file in which you have configured the providers and Azure objects:

```terraform
resource "celerdatabyoc_azure_deployment_credential" "example" {
  name                = "<deployment_credential_name>"
  application_id      = azuread_application_registration.example.client_id
  directory_id        = azuread_service_principal.app_service_principal.application_tenant_id
  client_secret_value = azuread_application_password.example.value
  ssh_key_resource_id = azurerm_ssh_public_key.example.id
}

resource "celerdatabyoc_azure_data_credential" "example" {
  name                         = "<data_credential_name>"
  managed_identity_resource_id = azurerm_user_assigned_identity.example.id
  storage_account_name         = azurerm_storage_account.example.name
  container_name               = azurerm_storage_container.example.name
}

resource "celerdatabyoc_azure_network" "example" {
  name                        = "<network_credential_name>"
  deployment_credential_id    = celerdatabyoc_azure_deployment_credential.example.id
  virtual_network_resource_id = azurerm_virtual_network.example.id
  subnet_name                 = azurerm_subnet.example.name
  region                      = local.cluster_region
  public_accessible           = true
}

resource "celerdatabyoc_classic_cluster" "azure_terraform_test" {
  cluster_name             = "<cluster_name>"
  fe_instance_type         = "<fe_node_instance_type>"
  fe_node_count            = 1
  deployment_credential_id = celerdatabyoc_azure_deployment_credential.example.id
  data_credential_id       = celerdatabyoc_azure_data_credential.example.id
  network_id               = celerdatabyoc_azure_network.example.id
  be_instance_type         = "<be_node_instance_type>"
  be_node_count            = 2
  be_disk_number           = 2
  be_disk_per_size         = 100
  default_admin_password   = "<SQL_user_initial_password>"

  expected_cluster_state = "Running"
  resource_tags = {
    flag = "terraform-test"
  }
  csp    = "azure"
  region = local.cluster_region
}
```

See the following documents for more information:

- [celerdatabyoc_azure_data_credential](../resources/azure_data_credential.md)
- [celerdatabyoc_azure_deployment_credential](../resources/azure_deployment_credential.md)
- [celerdatabyoc_azure_network](../resources/azure_network.md)
- [celerdatabyoc_classic_cluster](../resources/classic_cluster.md)

## Apply configurations

After you finish [configuring the providers](#configure-providers) and [describing the infrastructure objects](#describe-infrastructure) in your Terraform configuration, follow these steps to apply the configuration in your Terraform project:

1. Initialize and install the providers defined in the Terraform configuration:

   ```SQL
   terraform init
   ```

2. Verify that your Terraform project has been properly configured:

   ```SQL
   terraform plan
   ```

   If there are any errors, edit the Terraform configuration and re-run the preceding command.

3. Apply the Terraform configuration:

   ```SQL
   terraform apply
   ```

When the system returns a "Apply complete!" message, the Terraform configuration has been successfully applied.

~> After you change the provider versions in the Terraform configuration, you must run `terraform init -upgrade` to initialize the providers and then run `terraform apply` again to apply the Terraform configuration.

## Delete configurations

You can delete your Terraform configuration if you no longer need it.

Deleting a Terraform configuration means destroying all resources created by the CelerData Cloud Private provider.

To delete a Terraform configuration, run the following command in your Terraform project:

```SQL
terraform destroy
```

When the system returns a "Destroy complete!" message, the Terraform configuration has been successfully deleted and the cluster created by the Terraform configuration is also released.
