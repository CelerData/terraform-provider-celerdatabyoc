---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "celerdatabyoc_elastic_cluster Resource - terraform-provider-celerdatabyoc"
subcategory: ""
description: |-
  
---

~> The resource's API may change in subsequent versions to simplify the user experience.

This document can help you deploy an elastic cluster in AWS EC2. Please follow the runnable example on AWS.

### The list of node sizes
<table>
 <tbody>
  <tr>
   <td rowspan="2"></td>
   <td rowspan="2">Node size</td>
   <td rowspan="2">Node information</td>
  </tr>
  <tr height="17.55" style="height:17.55pt;">
  </tr>
  <tr>
   <td rowspan="7">Coordinator node size</td>
  </tr>
  <tr>
  </tr>
  <tr>
   <td>m6i.xlarge</td>
   <td>4 Cores 16GB Memory 50 GB Storage</td>
  </tr>
  <tr>
   <td>m6i.2xlarge</td>
   <td>8 Cores 32GB Memory 200 GB Storage</td>
  </tr>
  <tr>
   <td>m6i.4xlarge</td>
   <td>16 Cores 64GB Memory 50 GB Storage</td>
  </tr>
  <tr>
   <td>m6i.8xlarge</td>
   <td>32 Cores 128GB Memory 50 GB Storage</td>
  </tr>
  <tr>
  </tr>
  <tr>
   <td rowspan="7">Compute node size (General Purpose)</td>
   <td>m5d.2xlarge</td>
   <td>8 Cores 32GB Memory</td>
  </tr>
 </tbody>
</table>

### Example Usage

```terraform
locals {
  your_s3_bucket = "[your S3 bucket]" 
}

resource "celerdatabyoc_aws_data_credential_policy" "new" {
  bucket = local.your_s3_bucket
}


data "celerdatabyoc_aws_data_credential_assume_policy" "assume_role" {}

resource "aws_iam_role" "celerdata_data_cred_role" {
  name               = "celerdata_data_cred_role"
  assume_role_policy = data.celerdatabyoc_aws_data_credential_assume_policy.assume_role.json
  description        = "Celerdata Data Credential"
  inline_policy {
    name   = "celerdata_data_cred_role_policy"
    policy = celerdatabyoc_aws_data_credential_policy.new.json
  }
}

resource "aws_iam_instance_profile" "celerdata_data_cred_profile" {
  name = "celerdata_data_cred_profile"
  role = aws_iam_role.celerdata_data_cred_role.name
}

resource "celerdatabyoc_aws_deployment_credential_policy" "new" {
  bucket = local.your_s3_bucket
  data_role_arn = aws_iam_role.celerdata_data_cred_role.arn
}

resource "celerdatabyoc_aws_deployment_credential_assume_policy" "new" {}

resource "aws_iam_role" "deploy_cred_role" {
  name               = "deploy_cred_role"
  assume_role_policy = celerdatabyoc_aws_deployment_credential_assume_policy.new.json
  description        = "Celerdata Deploy Credential"
  inline_policy {
    name   = "deploy_cred_role-policy"
    policy = celerdatabyoc_aws_deployment_credential_policy.new.json
  }
}

resource "celerdatabyoc_aws_data_credential" "new" {
  name = "data-credential"
  role_arn = aws_iam_role.celerdata_data_cred_role.arn
  instance_profile_arn = aws_iam_instance_profile.celerdata_data_cred_profile.arn
  bucket_name = local.your_s3_bucket
  policy_version = celerdatabyoc_aws_data_credential_policy.new.version
}

resource "celerdatabyoc_aws_deployment_role_credential" "new" {
  name = "deployment-role-credential"
  role_arn = aws_iam_role.deploy_cred_role.arn
  external_id = celerdatabyoc_aws_deployment_credential_assume_policy.new.external_id
  policy_version = celerdatabyoc_aws_deployment_credential_policy.new.version
}

resource "celerdatabyoc_aws_network" "new" {
  name = "[name your net work]"
  subnet_id = "[your subnet id]"
  security_group_id = "[your security group id]"
  region = "[your AWS VPC region]"
  deployment_credential_id = celerdatabyoc_aws_deployment_role_credential.new.id
  vpc_endpoint_id = "[your vpc endpoint id]"
}


resource "celerdatabyoc_elastic_cluster" "elastic" {
  cluster_name = "[your cluster name]"
  coordinator_node_size = "[coordinator node size]"
  coordinator_node_count = 1
  deployment_credential_id = celerdatabyoc_aws_deployment_role_credential.new.id
  data_credential_id = celerdatabyoc_aws_data_credential.new.id
  network_id = celerdatabyoc_aws_network.new.id
  compute_node_size = "[compute node size]"
  compute_node_count = 1
  default_admin_password = "[initial SQL user pwd]"
  expected_cluster_state = "[type cluster state]"
  resource_tags = {
    celerdata = "test"
  }
  csp = "aws"
  region = "[your aws vpc region]"

  init_scripts {
      logs_dir    = "log-s3-path/"
      script_path = "script-s3-path/test.sh" 
  }
  run_scripts_parallel = false
}
```

### Argument Reference

 * `cluster_name` - (ForceNew) Name your cluster.
 * `coordinator_node_size` - (ForceNew) Select the coordinator node size from the table above.
 * `coordinator_node_count` - (Optional) Default number of coordinator nodes is 1, optional numbers are: `1,3,5`.
 * `deployment_credential_id` - (ForceNew) Should type as `celerdatabyoc_aws_deployment_role_credential.new.id`.
 * `data_credential_id` - (ForceNew) Should type as `celerdatabyoc_aws_data_credential.new.id`.
 * `network_id` - (ForceNew) Should type as `celerdatabyoc_aws_network.new.id`.
 * `compute_node_size` - (ForceNew) Should select a compute node size from the table above.
 * `compute_node_count` - (Required) Set the number of compute node.
 * `default_admin_password` - (Required) Set initial SQL user password.
 * `expected_cluster_state` - (Required) When creating a cluster, you need to declare whether the cluster status is `Suspended` or `Running`.
 * `resource_tags` - (Optional)
 * `csp` - (Required) Now, we only support `aws`
 * `region` - (Required) Your AWS VPC region. The optional regions are as follows：
    - Asia Pacific (Singapore) ap-southeast-1
    - US East (N. Virginia) us-east-1
    - US West (Oregon) us-west-2
    - Europe (Ireland) eu-west-1
    - Europe (Frankfurt) eu-central-1
 * `init_scripts` -（Optional）Configuration block to customize the script upload location. The maximum number of executable scripts is `20`. You can learn more about executable scripts with Run scripts.
    - logs_dir - (ForceNew) Storage path for script execution results.
    - script_path - (ForceNew) The S3 bucket address where the script is stored.
 * `run_scripts_parallel` - (Optional) Execute/not execute script in parallel, the default value is `false`.
 * `query_port` - (Optional) Customize cluster endpoints query port, the port range should be 1-65535 and cannot be
  443, default value is 9030.
### Supplementary material

[The AWS IAM](https://us-east-1.console.aws.amazon.com/iamv2/home?region=us-east-1#/policies)<br />
[How to create AWS data credential](https://docs-sandbox.celerdata.com/en-us/main/cloud_settings/manage_storage_configurations)<br />
[How to create the AWS Deployment credential](https://docs-sandbox.celerdata.com/en-us/main/cloud_settings/manage_credentials)<br />
[Create a network configuration](https://docs-sandbox.celerdata.com/en-us/main/cloud_settings/manage_network_configurations)
